import os

rootpath = Dir('../../').abspath
extpath = os.path.join(rootpath, 'ext')

print("Integration Test")

envGlobal = Environment(toolpath=[os.path.join(extpath, 'scons-build-tools/site_tools')],
                        tools=['compiler_sparc_rtems_gcc', 'settings_buildpath', 'utils_buildsize', 'utils_buildformat'],
                        OS='rtems',
                        BOARD='nexys3',
                        DEVICE_SIZE={
                            'name' : 'Eu:CROPIS CDH OBC SDM',
                            'flash': 2097151,
                            'ram'  : 134217728,
                        },
                        ENV=os.environ)

buildfolder = os.path.join(rootpath, 'release/it_build/')
envGlobal['RTEMS_ROOT_FOLDER'] = '/opt/rtems-4.12'
envGlobal['COMPILERPATH'] = '$RTEMS_ROOT_FOLDER/bin/'
envGlobal['RTEMS_BSP_FOLDER'] = '$RTEMS_ROOT_FOLDER/sparc-rtems4.12/leon3/lib'
envGlobal['COMPILERPREFIX'] = 'sparc-rtems4.12-'
envGlobal.Append(CCFLAGS_target = [
    #'-I$RTEMS_ROOT_FOLDER/include/',
    '-B$RTEMS_BSP_FOLDER',
    '-I$RTEMS_BSP_FOLDER/include/',
    '--specs=$RTEMS_BSP_FOLDER/bsp_specs',
    '-qrtems',
    '-v',
])

envGlobal.Tool('compiler_sparc_rtems_gcc')
envGlobal.Tool('settings_buildpath')
envGlobal['BASEPATH'] = rootpath 
envGlobal['BUILDPATH'] = os.path.abspath(buildfolder)
envGlobal['OUTPOST_CORE_PATH'] = os.path.join(extpath, 'outpost-core')
envGlobal['CXXFLAGS_language'] = ['-std=gnu++11']


envGlobal.Append(CPPPATH=[
    rootpath,
    os.path.abspath(os.path.join(rootpath, "ds")),
])

envGlobal.SConscript([
        os.path.join(rootpath, 'ds/paffs/SConscript'),
        os.path.join(rootpath, 'ds/paffs/driver/SConscript.nulldriver'),
        
    ],
    exports=['envGlobal'])

env = envGlobal.Clone()

files = env.Glob('*.cpp')

programElf = env.Program('office.elf', files)
programBin = env.Bin(programElf) 

envGlobal.Alias('build', programBin)
envGlobal.Install('$BUILDPATH', programElf)
envGlobal.Install('$BUILDPATH', programBin)
envGlobal.Default('build')
